prog ::= decl_list 'EOF'

/* Declarations */

decl_list ::= /* epsilon */
    | decl decl_list

decl ::= 'ID' ':' type_simple ';'
    | 'ID' ':' type_array_sized ';'
    | 'ID' ':' type_func ';'
    | decl_init

decl_init ::= 'ID' ':' type_simple '=' expr ';'
    | 'ID' ':' type_array_sized '=' '{' opt_expr_list '}' ';'
    | 'ID' ':' type_func '=' '{' opt_stmt_list '}'

/* Statements */

opt_stmt_list ::= /* epsilon */
    | stmt_list

stmt_list ::= stmt
    | stmt stmt_list

stmt ::= open_stmt
    | closed_stmt

closed_stmt ::= if_stmt_closed
    | for_stmt_closed
    | while_stmt_closed
    | simple_stmt

open_stmt ::= if_stmt_open
    | for_stmt_open
    | while_stmt_open

if_cond ::= 'IF' '(' opt_expr ')'

if_stmt_closed ::= if_cond closed_stmt 'ELSE' closed_stmt

if_stmt_open ::= if_cond stmt
    | if_cond closed_stmt 'ELSE' if_stmt_open

for_header ::= 'FOR' '(' opt_expr ';' opt_expr ';' opt_expr ')'

for_stmt_open ::= for_header open_stmt

for_stmt_closed ::= for_header closed_stmt

while_stmt_open ::= 'WHILE' '(' expr ')' open_stmt

while_stmt_closed ::= 'WHILE' '(' expr ')' closed_stmt

/* Simple statements are not recursive */
simple_stmt ::= print_stmt
    | return_stmt
    | block_stmt
    | decl
    | expr ';'

print_stmt ::= 'PRINT' opt_expr_list ';'

return_stmt ::= 'RETURN' opt_expr ';'

block_stmt ::= '{' stmt_list '}'

/* Expressions */

opt_expr_list ::= /* epsilon */
    | expr_list

expr_list ::= expr
    | expr ',' expr_list

opt_expr ::= /* epsilon */
    | expr

expr ::= expr1

expr1 ::= lval '=' expr1
    | expr2

lval ::= 'ID'
    | 'ID' index

expr2 ::= expr2 'LOR' expr3
    | expr3

expr3 ::= expr3 'LAND' expr4
    | expr4

expr4 ::= expr4 'EQ' expr5
    | expr4 'NE' expr5
    | expr4 'LT' expr5
    | expr4 'LE' expr5
    | expr4 'GT' expr5
    | expr4 'GE' expr5
    | expr5


expr5 ::= expr5 '+' expr6
    | expr5 '-' expr6
    | expr6

expr6 ::= expr6 '*' expr7
    | expr6 '/' expr7
    | expr6 '%' expr7
    | expr7

expr7 ::= expr7 '^' expr8
    | expr8

expr8 ::= '-' expr8
    | 'NOT' expr8
    | expr9

expr9 ::= expr9 'INC'
    | expr9 'DEC'
    | group

assign_op ::= '+=' | '-=' | '*=' | '/='

assign_expr ::= lval assign_op expr

incdec_op ::= '++' | '--'

incdec_expr ::=
      incdec_op lval
    | lval incdec_op

expr_plus ::= incdec_expr
            | assign_expr
            | expr

group ::= '(' expr ')'
    | 'ID' '(' opt_expr_list ')'
    | 'ID' index
    | factor

index_list ::= index_list index
		| index

index ::= '[' expr ']'

factor ::= 'ID'
    | 'INTEGER_LITERAL'
    | 'FLOAT_LITERAL'
    | 'CHAR_LITERAL'
    | 'STRING_LITERAL'
    | 'TRUE'
    | 'FALSE'

/* Types */

type_simple ::= 'INTEGER'
    | 'FLOAT'
    | 'BOOLEAN'
    | 'CHAR'
    | 'STRING'
    | 'VOID'

type_array ::= 'ARRAY' '[' ']' type_simple
    | 'ARRAY' '[' ']' type_array

type_array_sized ::= 'ARRAY' index type_simple
    | 'ARRAY' index type_array_sized

type_func ::= 'FUNCTION' type_simple '(' opt_param_list ')'
    | 'FUNCTION' type_array_sized '(' opt_param_list ')'

opt_param_list ::= /* epsilon */
    | param_list

param_list ::= param
    | param_list ',' param

param ::= 'ID' ':' type_simple
    | 'ID' ':' type_array
    | 'ID' ':' type_array_sized